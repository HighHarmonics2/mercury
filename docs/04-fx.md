# Sound Effects (fx)

The sound FX currently have a fixed chain when using the Mercury standalone version in Max. The chain of effects is as following:

```
synth / sample / polySynth / loop (mono)
|
freeze (spectral freezer)
|
shift (pitchshifter)
|
vibrato (pitch modulation)
|
triggerFilter (envelope modulated filter)
|
kink (distortion)
|
drive (distortion)
|
filter (low/hi/band filter with optional LFO modulation)
|
comb (combfiltering)
|
degrade (downsampling)
|
squash (basic compression/distortion)
|
gain (the volume of the sound) -> sender
|
panning
||
double / chorus
||
|| >>====== vv
||          +
||          hall (stereo reverb)
++ <<====== <<
||
lfo (low frequency oscillator)
||
delay (stereo pingpong delay + drywet)
||
output
```

```
|  mono
-  mono
|| stereo
=  stereo
++ sum
```

## reverb

Add a reverberation effect to the sound, making it sound like it's in a room or big hall. The effect has 2 arguments, the amplitude of the reverb and the reverb-size. The amplitude is specified as floating-point amplitude and the length is a value between `0` and `18` (choosing between 19 presets). The default is `0.5` amplitude and `10` length.

**arguments**
- {Float+} -> amplitude of reverb (default=0.5)
- {Number+} -> size of reverb 0-18 (default=10)
- {Number+} -> slide time in ms when size is changed (default=10)
- {Float+} -> drywet 0-1 (optional, 1=100% wet, default=0.5)

Alias: `fx(hall)`

```
fx(reverb)
fx(reverb <size>)
fx(reverb <amount> <size>)
fx(reverb <amount> <size> <slide> <dry-wet>)
```

```java
new sample snare_909 time(1) fx(reverb 0.93 15)
```

## delay

Add a pingpong delay effect to the sound. The effect has 4 arguments, the left delaytime, the right delaytime, the feedback and the damping. The delaytime is specified in bar time divisions, where `0.25` (or `1/4`) is a quarter note. The feedback is specified as a value between `0` and `1`. Positive feedback is allowed and internally compressed/distorted, but watch out for your ears just in case. The damping is a onepole lowpass filter for which the cutoff is specified between `0` and `1` where 0 means full damping, and 1 means none (filter open).

Alias: `fx(echo)`

**arguments**
- {Division} -> delaytime for left (optional, default=3/16)
- {Division} -> delaytime for right (optional, default=2/8)
- {Float+} -> feedback 0-1 (optional, default=0.8)
- {Float+} -> damping 0-1 (optional, default=0.5)
- {Float+} -> drywet 0-1 (optional, 1=100% wet, default=0.5)

```
fx(delay) (defaults)
fx(delay <timeLR>)
fx(delay <timeLR> <fb>)
fx(delay <timeL> <timeR> <fb>)
fx(delay <timeL> <timeR> <fb> <damp>)
fx(delay <timeL> <timeR> <fb> <damp> <drywet>)
```

```java
new sample hat_909 time(1/2) fx(delay 3/16 7/16 0.5 0.7)
```

## filter

Add a filter to the sound. This can be a static filter or a modulated filter depending on the amount of arguments you provide. The filter-type can be a `low`, `high` or `band`-pass filter. The second argument is the cutoff frequency in Hz and the third argument is the resonance between `0` and `1`. Passing a single argument only sets the cutoff, passing two arguments sets the cutoff and resonances. The filter then defaults to `lowpass`.

**arguments**
- {Name} -> filter type, low, high, band (default=low)
- {Number+} -> cutoff frequency in Hz (default=1200)
- {Float+} -> resonance between 0-1 (default=0.45)

```
fx(filter)
fx(filter <cutoff>)
fx(filter <cutoff> <resonance>)
fx(filter <type> <cutoff> <resonance>)
```

```java
new synth saw note(0 1) shape(-1) fx(filter low 800 0.6)
```

You can provide extra arguments to have the filter modulate between a low and high cutoff-frequency. In this case the arguments are as follows (in order): The filter-type `low / high / band`. The modulation speed in float/expression as musical time division where `0.25` or `1/4` = a quarter note. The high cutoff in Hz, the low cutoff in Hz the resonance between `0` and `1`. The modulation shape tilt between `0` and `1`, where 0 is ramp-down, 1 is ramp-up and 0.5 is triangle form. The exponential curve for the filter as floating point.

**arguments**
- {Name} -> filter type, low, high, band (default=low)
- {Division} -> modulation ratio (default=1/1)
- {Number+} -> low modulation frequency in Hz (default=200)
- {Number+} -> high modulation frequency in Hz (default=3000)
- {Float+} -> resonance between 0-1 (default=0.45)
- {Float+} -> modulation slope 0-1 up, down, triangle) (default=0.5)
- {Float+} -> exponential scaling (default=2)

```java
new synth saw note(0 0) fx(filter low 1/4 100 3500 0.55 0 4)
```

## triggerFilter

A filter with an envelope that is controlled by the trigger of the instrument. Whenever the sequencer triggers an event that would cause the instrument to sound this will also be the event that triggers the start of this filter. The filter has various types, a low and high frequency value for the envelope to move between, an attack and release time in ms or division value and an optional exponent for the slope of the filter.

**arguments**
- {Name} -> filter type, `low`, `high`, `band` (default=low)
- {Number+/Division} -> attack time in ms or division (default=1)
- {Number+/Division} -> release time in ms or division (default=1/16)
- {Number+} -> high frequency point in the envelope (default=4000)
- {Number+} -> low frequency point in the envelope (default=100)
- {Float+} -> exponent for the envelope curve (default=1)

```java
list beat euclid(8 3)
new synth saw note(0 1) time(1/8) play(beat) fx(triggerFilter low 10 1/4 1000 150 0.5)
```

## double / chorus

Add an Automated-Double-Tracking (ADT) or Chorus effect to the sound. When only typing double it will sound like two versions of the sound are created, one left and one right. When typing chorus the original sound is added and the delaytimes are longer. Arguments are the modulation speed in division, the modulation depth in milliseconds, the modulation wave `sine` or `random`, the dry-wet ratio between `0` and `1` and a detune factor between left and right modulation wave in floating.

**arguments**
- {Division} -> modulation rate (default=4/1)
- {Number+} -> modulation depth in ms (default=8 for double, default=45 for chorus)
- {Name} -> modulation wave, sine or random (default=random)
- {Float+} -> dry-wet (default=1 for double, default=0.5 for chorus)
- {Float+} -> modulation rate detune (default=0.94562)

```
fx(double)
fx(chorus)
fx(chorus <rate>)
fx(chorus <rate> <depth> <wave> <wet-dry> <detune>)
```

**browser version only:**
- {Division} -> modulation rate (default=8/1)
- {Number+} -> modulation depth in ms (default=8 for double, default=45 for chorus)
- {Float+} -> dry-wet (default=1 for double, default=0.5 for chorus)

```
fx(double)
fx(chorus)
fx(chorus <rate>)
fx(chorus <rate> <depth> <wet-dry>)
```

```java
new sample clap_808 fx(double)

new synth saw fx(chorus 5/1 30)
```

## freeze

Create an FFT-freeze effect on the sound. This will result in a "frozen-in-time"-like sound, where the timbre is still clearly audible. The freezer is triggered at the time interval of the `time()` function. The trigger can be set with a float to give it a probability of triggering, or a list can be provided to let the freezing occor in a rhythmic pattern. The second optional argument determines the sliding time between 2 freeze frames (in n-frames), and the third optional argument sets the amount of frames to store.

**arguments**
- {Number} -> freeze rhythm or probability (default=1)
- {Int} -> interpolation between frames (default=0)
- {Int} -> number of frames stored in freezer (default=8)

```java
list playHarp [1 0 0 0 0 0 0 0]
list trigger [1 0 0 0 0]

new sample harp_up time(1/4) beat(playHarp) name(harp)
	set harp fx(freeze trigger)
```

## shift

Make a time-domain pitchshifting effect to change the pitch of the sound. The first argument sets the shift in semitones (this can also be a floating-point value to create microtonal shifts). The second value sets the wet-dry ratio (default = 1) to combine the original with the shifted sound. You can change the shifting quality (higher quality sounds better but uses more cpu).

**arguments**
- {Number} -> shifting factor (positive/negative) in semitones (optional, default=-12)
- {Float} -> dry-wet mix between 0-1 (optional, default=1)
- {Name} -> shifting settings `basic`, `good`, `better`, `best` (optional, default=basic) 

```java
new sample violin_c time(1/4) fx(shift 7 0.5)
```

## vibrato

Add a vibrato effect to the incoming signal by modulating a short delayline with a sinewave. The depth sets the delayline length in ms, the rate sets the frequency of the sinewave in Hz.

**arguments**
- {Number+} -> vibrato rate in Hz (optional, default=2)
- {Number+} -> vibrato depth in ms (optional, default=5)

```java
new sample violin_c time(1/4) fx(vibrato 4 5)
```

## squash

A simple compression/distortion algorithm based on a design by Peter McCulloch. Raise the amount to squash the incoming signal, first creating some kind of simple compression, but quickly introduces nice distortion harmonics.

**arguments**
- {Number+} -> input gain amount, greater than 1 (optional, default=4)
- {Float+} -> dry-wet factor 0-1 (optional, default=1)
- {Float} -> compression factor greater than 0 (optional, default=0.28)

```java
new sample piano_e time(1) fx(squash 2.3)
```

## kink

A kink distortion algorithm. Creates a bend or "kink" in the audio signal.  

**arguments**
- {Number+} -> distortion amount, greater then 0 (optional, default=5)
- {Float+} -> dry-wet factor 0-1 (optional, default=1)

```java
new sample violin_c time(1) fx(kink 15)
```
## distort

A distortion/overdrive/soft-clipping effect. Uses the `tanh()` algorithm.

Alias: `fx(drive)`

**arguments**
- {Number+} -> distortion amount, greater then 0 (optional, default=2)
- {Float+} -> dry-wet factor 0-1 (optional, default=1)

```java
new sample kick_909 fx(drive 15)
```

## degrade

A downsampling/8bit/chiptune effect. The signal is downsample by a factor, where 0 means no downsampling (original samplerate) and 0.5 is half the samplerate etc (e.g. 44100 * 0.5 = 22050). 

Alias: `fx(chip)`

**arguments**
- {Float+} -> downsampling amount between 0-1
- {Float+} -> dry-wet factor 0-1 (optional, default=1)

```java
new synth saw fx(degrade 0.75)
```

## comb

A combfilter effect. This combines a small delayed version of the sound with the original sound resulting in a combfilter, giving a metallic quality to the sound. The first argument is the semitone the filter is tuned at (synced to the scale and allowing for detuning with floating points), the second optional argument is a feedback amount.

**arguments**
- {Number+} -> filter frequency as note semiton (default=0)
- {Float+} -> filter feedback between 0 and 0.99 (default=0.8)
- {Float+} -> dry-wet factor 0-1 (optional, default=0.5)

```java
new synth saw fx(comb 82 0.4)
```

## lfo

Add a Low Frequency Oscillator effect to the sound. This results in a rapidly fading in/out sounding effect (helicopter sound). Various arguments allow to shape the speed and type of wave for modulation. The first argument is the rate in division. The second argument is the shape type `square`, `up`, `down` or  `sine`. With the third argument you can change the lowest amplitude of the modulation and with the optional fourth argument you can change the width (only works with the square)

Alias: `fx(tremolo)`

**arguments**
- {Division} -> speed of the LFO (default=1/16)
- {Name} -> type of the wave, square, up, down or sine (default=square)
- {Float+} -> dry-wet factor 0-1 (optional, default=1)
- {Float+} -> width of the square wave between 0-1 (optional, default=0.5)
- {Float+} -> offset of the waveform 0-1 (optional, default=0)

```
fx(lfo)
fx(lfo <time-division>)
fx(lfo <time> <type>)
fx(lfo <time> <type> <range> <width>)
```

```java
new loop amen fx(lfo 1/16)
```

## vocoder

A classic vocoder effect consisting of 16 resonant bandpass filters spread logarithmically between a low and high frequency range with a specified resonance Q factor. The input sound will be analyzed through the bands with an amplitude follower (with variable release time) and the carrier sound will be used to output the synthesized result. The carrier is noise by default, but can be set to receive from an other named synth or sample. For this use the `name()` method. Optionally a threshold can adjust which bands are passed through, only if their amplitude is loud enough.

**arguments**
- {Name} -> carrier source name of other instrument (default=noise)
- {Number+} -> bandpass filter resonance 0-100 (default=15)
- {Number+} -> amplitude following release time in ms (default=50)
- {Float+} -> wet dry ratio 0-1 (default=1)
- {Number+} -> lowest frequency of the filterbank (default=50)
- {Number+} -> highest frequency of the filterbank (default=10000)
- {Float+} -> band threshold 0-1 (default=0)

```java
new synth saw name(bass) note(0 0) shape(-1) gain(1 0 1)

new loop amen fx(vocoder bass 20 100 1 50 5000)
```